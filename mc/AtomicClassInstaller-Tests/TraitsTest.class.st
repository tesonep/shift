Class {
	#name : #TraitsTest,
	#superclass : #AbstractAtomicInstallerTest,
	#category : 'AtomicClassInstaller-Tests'
}

{ #category : #'as yet unclassified' }
TraitsTest >> testNewClassWithTrait [
	| class |

	self assert: (TComparable users noneSatisfy: [ :e | e name = #NewClassWithTrait]) .

	class := AtomicClassInstaller
		make: [ :builder | 
			builder
				name: #NewClassWithTrait;
				traitComposition: TComparable;
				slots: #();
				category: 'AtomicClassInstaller-Tests' ].
	
	self assert: (TComparable users noneSatisfy: [ :e | e name = #NewClassWithTrait]).
	self deny: (Object subclasses includes: class).
	self deny: (self environment includesKey: #NewClassWithTrait).
	
	self assertNonAnnouncements.
	
	AtomicClassInstaller commit.
	
	self assert: (TComparable users includes: self newClassWithTrait) .
	self assert: (self newClassWithTrait canUnderstand: #max:).
	self assert: (self newClassWithTrait canUnderstand: #min:).	
	self assert: (Object subclasses includes: self newClassWithTrait).

]

{ #category : #'as yet unclassified' }
TraitsTest >> testRemoveTrait [
	| class |

	self assert: (TComparable users includes: self oldComparableClass) .
	self assert: (self oldComparableClass canUnderstand: #max:).
	self assert: (self oldComparableClass canUnderstand: #min:).	

	class := AtomicClassInstaller
		make: [ :builder | 
			builder
				name: #OldComparableClass;
				traitComposition: { };
				slots: #(firstVariable);
				category: 'AtomicClassInstaller-Tests' ].
	
	self assert: (TComparable users includes: self oldComparableClass) .
	self assert: (self oldComparableClass canUnderstand: #max:).
	self assert: (self oldComparableClass canUnderstand: #min:).	
	self deny: (Object subclasses includes: class).
	
	self assertNonAnnouncements.
		
	AtomicClassInstaller commit.
	
	self deny: (TComparable users includes: self oldComparableClass) .
	self deny: (self oldComparableClass canUnderstand: #max:).
	self deny: (self oldComparableClass canUnderstand: #min:).	
	self assert: (Object subclasses includes: class).

]
